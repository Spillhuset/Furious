plugins {
    id 'java'
    id('xyz.jpenilla.run-paper') version '2.3.1'
}

group = 'com.spillhuset'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.9-R0.1-SNAPSHOT")
    implementation("org.xerial:sqlite-jdbc:3.46.0.0")
    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    implementation("com.zaxxer:HikariCP:7.0.2")
    implementation("com.mysql:mysql-connector-j:9.2.0")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.9")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// --- Added by automation: export plugin jar configuration ---
jar {
    // Ensure the artifact name is consistent
    archiveBaseName.set('Furious')
}

tasks.register('exportPlugin', Copy) {
    // This task collects the built jar into a distributable folder
    doNotTrackState("simple distribution copy")
    dependsOn 'build'
    from("${buildDir}/libs")
    include("*.jar")
    into("${projectDir}/dist")
    // Copy with a consistent file name for uploading
    rename { String fileName -> "Furious.jar" }
}
